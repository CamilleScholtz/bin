#!/bin/fish
#
# neet - A script to easily play and manage your anime/kdrama/etc.
# onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set media and config dir locations
set configdir $HOME/.neet
set mediadir $HOME/media/videos

# Pause mpd on play (requires mpc)
set pausempd true

# mpv launch options
set options --fullscreen

# Set highlight color
set highlight yellow


## FUNCTIONS

# This function gets basic media info, like the active show name, episode count and total episodes
function info
	# Get the active show (+)
	set active (grep -B 1 '+' $configdir/list)

	# Get the show name, current episode and total episode count
	set -g name $active[1]

	if test -z $current
		set -g current (string match -r -a '[0-9]+' $active[2] | head -n 1)
	end
	set -g total (string match -r -a '[0-9]+' $active[2] | tail -n 1)
end

# This function tries to fuzzy find the right directory
function directory
	# Check if the show already has an alias
	if test -n (grep "^$name=" $configdir/alias) -a $realias = 'false' ^ /dev/null
		set -g path (grep "^$name=" $configdir/alias | string replace -r '.*=' '')
		return
	end

	# Lists all directories in mediadir
	set -g directories (ls -d $mediadir/* | string replace -r '.*/' '')
	
	# This is the actual fuzzy logic part, it compares the directory names with the active show name
	function fuzzycompare
		for directory in $directories
			set match 0

			for word in (string split ' ' $name)
				set wordmatch (string match -r -i -a -n $word $directory | string replace -r '.* ' '')
				if test $status -ge 1
					continue
				end
				set wordmatch (string join ' + ' $wordmatch)

				set match (math $match + $wordmatch)
			end

			echo $match $directory
		end
	end

	# This gets the output with the most matches
	set matches (fuzzycompare | sort -n)
	set -g path (echo $matches[-1] | cut -d ' ' -f 2-)
	
	# Create alias
	if test -n $path
		echo 'Show initialized. (alias set)'
		echo $name=$path > $configdir/alias
	# Throw an error if there are no directory matches
	else 
		echo 'No '(set_color $highlight)'directory'(set_color normal)' match.'
		exit 1
	end
end

# This function tries to get the right file name
function episode
	# Lists all video files in the directory
	set episodes (find $mediadir/$path -type f -regex '.*\.\(avi\|mkv\|mp4\|avi.part\|mkv.part\|mp4.part\)' | string replace -r '.*/' '')

	# Sets the right file (hopefully)
	# TODO: Simplify this
	# E01
	set -g file (string match -r -i '.*E[0]*'$current'[^0-9]?.*$' $episodes)
	# EP01
	if test $status -ge 1
		set -g file (string match -r -i '.*EP[0]*'$current'[^0-9]?.*$' $episodes)
		# 01
		if test $status -ge 1
			set -g file (string match -r -i '.* [0]*'$current'[^0-9]?.*$' $episodes)
			# Last resort (just the episode number)
			if test $status -ge 1
				set -g file (string match -r -i '.*[0]*'$current'[^0-9]?.*$' $episodes)
			end
		end
	end

	# Throw error if no file is found
	if test -z "$file"
		echo 'No '(set_color $highlight)'episode'(set_color normal)' match.'
		exit 1
	end
end

# This function pauses mpd, and starts mpv
function play
	echo "Playing episode $current/$total, "(set_color $highlight)"$name"(set_color normal)"."
	if test -n (string match -r '.part$' $file) ^ /dev/null
		echo 'This episode has not finished downloading yet.'
		echo 'Playback might be buggy or not work at all!'
	end

	# Pause mpd
	if test $pausempd = true
		mpc -q pause
	end

	# Start mpv
	mpv $options $mediadir/$path/$file
end


## EXECUTE

set realias false

if test (count $argv) -eq 1
	switch $argv[1]
		case -h --help
			echo -e 'Usage: neet [options] [-/+]\n'
			echo 'options:'
			echo '  -e,   --edit            edit list'
			echo '  -n,   --no-resume       disable resume playback'
			echo '  -r,   --realias         forgets current alias'
			echo '  -h,   --help            print help and exit'
			exit 0
		case -e --edit
			eval $EDITOR $configdir/list
			exit 0
		case -n --no-resume
			info

			set options $options --no-resume-playback
		case -r --realias
			info

			set realias true
		case '+*'
			# Fail if there is a non + character in argv
			if test -n (string replace -a '+' '' $argv[1])
				echo 'Invalid option, use -h for help.'
				exit 1
			end

			info

			# Get current episode and increment it by one
			set amount (string length $argv[1])
			set increment (math $current + $amount)

			if test $increment -le $total
				sed -i "s/+ $current\/$total/+ $increment\/$total/" "$configdir/list"
				set current $increment
			else
				echo "Episode $total is the last episode."
				exit 1
			end
		case '-*'
			# Fail if there is a non - character in argv
			# TODO: Use string here
			if test -n (echo $argv[1] | sed 's/-//g')
				echo 'Invalid option, use -h for help.'
				exit 1
			end

			info

			# Get current episode and decrement it by one
			# TODO: Use string here as well
			set amount (echo $argv[1] | wc -m)
			set decrement (math $current + 1 - $amount)

			if test $decrement -ge 1
				sed -i "s/+ $current\/$total/+ $decrement\/$total/" $configdir/list
				set current $decrement
			else
				echo 'There is no episode 0.'
				exit 1
			end
		case '*'
			echo 'Invalid option, use -h for help.'
			exit 1
	end
else
	info
end

directory
episode
play
