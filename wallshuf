#!/bin/fish
#
# wallshuf - Set random wallpapers.
# # onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set wallshuf location
set wallshufdir $HOME/.wallshuf

# Set start times, accepted wallpaper type (currently only one allowed), and background color
set morning 6 center DCD5BF
set afternoon 13 center DCD5BF
set night 20 center 394953

# Set highlight color
set highlight yellow


## FUNCTIONS

# This functions sets the wallpaper
function setrandomwall
	# Exit if the wallshuf dir does not exist
	if not test -d $wallshufdir/$argv[1]
		echo "No wallpapers in "(set_color $highlight)"$wallshufdir/$argv[1]"(set_color normal)"."
		exit 1
	end

	set wallpaper (ls $wallshufdir/$argv[1] | shuf -n 1)

	setroot -c --bg-color \#$argv[2] $wallshufdir/$argv[1]/$wallpaper
	echo "A $argv wallpaper has been set, "(set_color $highlight)"$wallpaper"(set_color normal)"."
end


## EXECUTE

if test (count $argv) -eq 1
	setrandomwall $argv
else
	# Get time
	set hour (date +%H)

	if test $hour -ge $morning[1] -a $hour -lt $afternoon[1]
		setrandomwall $morning[2] $morning[3]
	else if test $hour -ge $afternoon[1] -a $hour -lt $night[1]
		setrandomwall $afternoon[2] $afternoon[3]
	else
		setrandomwall $night[2] $night[3]
	end
end
