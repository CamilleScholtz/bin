#!/bin/fish
#
# wallshuf - Set random wallpapers.
# # onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set wallshuf location
set wallshufdir $HOME/.wallshuf

# Set setroot command (for example hsetroot [OPTIONS], feh [OPTIONS], etc.
set command setroot -c

# Set start times and accepted wallpaper types (currently only one allowed)
set morning 6 bright
set afternoon 13 bright
set night 20 dark

# Set highlight color
set highlight yellow


## FUNCTIONS

# This functions sets the wallpaper
function setrandomwall
	# Exit if the wallshuf dir does not exist
	if not test -d $wallshufdir/$argv
		echo "No wallpapers in "(set_color $highlight)"$wallshufdir/$argv"(set_color normal)"."
		exit 1
	end

	set wallpaper (ls $wallshufdir/$argv | shuf -n 1)

	eval $command $wallshufdir/$argv/$wallpaper
	echo "A $argv wallpaper has been set, "(set_color $highlight)"$wallpaper"(set_color normal)"."
end


## EXECUTE

if test (count $argv) -eq 1
	setrandomwall $argv
else
	# Get time
	set hour (date +%H)

	if test $hour -ge $morning[1] -a $hour -lt $afternoon[1]
		setrandomwall $morning[2]
	else if test $hour -ge $afternoon[1] -a $hour -lt $night[1]
		setrandomwall $afternoon[2]
	else
		setrandomwall $night[2]
	end
end
