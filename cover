#!/bin/fish
#
# cover -  A small script to easily resize and view album art
# onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set music dir location
set musicdir $HOME/media/music

# Set cover size
set size 160x160


## EXECUTE

if test (count $argv) -eq 1
	switch $argv[1]
		case -h --help
			echo -e "Usage: cover [options]\n"
			echo "options:"
			echo "  -a,   --all             resize all covers"
			echo "  -c,   --current         resize current playing album cover"
			echo "  -h,   --help            print help and exit"
			exit 0
		case -a --all
			set path (find $musicdir -type f -name cover.\* | sort)

			set all (count $path)
			set current 0

			# TODO: Fix blank line after read
			read -n 1 -p 'echo "Do you want to resize $all covers to $size? [y/N] "' answer
			switch $answer
				case Y y
					for line in $path
						set current (math $current + 1)
						set album (echo $line | cut -d / -f 7 | cut -d \  -f 3-)
						set basename (echo $line | rev | cut -d . -f 2- | rev)

						echo "Resizing cover $current/$all, "(set_color yellow)"$album"(set_color normal)"."
						convert $line -filter Lanczos -thumbnail $size! "$basename"_popup.png ^ /dev/null
						if test $status -ge 1
							exit 1
						end
					end
				case "*"
					exit 0
			end
		case -c --current
			set path (mpc -f \%file\% | cut -d \n -f 1 | rev | cut -d / -f 2- | rev)

			convert $musicdir/$path/cover.* -filter Lanczos -thumbnail $size! $musicdir/$path/cover_popup.png > /dev/null

			if test $status -eq 0
				echo "Cover resized successfully."
			else
				echo "This album doesn't have a cover."
				exit 1
			end
		case "*"
			echo "Invalid option, use -h for help."
			exit 1
	end
else
	set path (mpc -f \%file\% | cut -d \n -f 1 | rev | cut -d / -f 2- | rev)

	pqiv $musicdir/$path/cover.*

	if test $status -ge 1
		echo "This album doesn't have a cover."
		exit 1
	end
end
