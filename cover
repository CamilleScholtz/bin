#!/bin/fish
#
# cover -  A small script to easily resize and view album art
# onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set music dir location
set musicdir $HOME/media/music

# Set cover size
set size 128x128

# Set highlight color
set highlight yellow


## EXECUTE

if test (count $argv) -eq 1
	switch $argv
		case -h --help
			echo -e 'Usage: cover [options]\n'
			echo 'options:'
			echo '  -a,   --all             resize all covers'
			echo '  -c,   --current         resize current playing album cover'
			echo '  -h,   --help            print help and exit'
			exit 0
		case -a --all
			set covers (find $musicdir -type f -name 'cover.*' | sort)

			set all (count $covers)
			set current 0

			# TODO: Fix blank line after read
			read -n 1 -p 'echo "Do you want to resize $all covers to $size? [y/N] "' answer
			switch $answer
				case Y y
					for cover in $covers
						set current (math $current + 1)
						set path (string split '/' $cover)

						echo "Resizing cover $current/$all, "(set_color $highlight)"$path[-2]"(set_color normal)"."
						convert $cover -filter Lanczos -thumbnail $size! (string replace -r 'cover.(jpg|png|gif)' 'cover_popup.png' $cover)
						if test $status -ge 1
							exit 1
						end
					end
				case '*'
					exit 0
			end
		case -c --current
			set path (mpc -f '%file%' | cut -d \n -f 1 | rev | cut -d / -f 2- | rev)

			convert $musicdir/$path/cover.* -filter Lanczos -thumbnail $size! $musicdir/$path/cover_popup.png >/dev/null

			if test $status -eq 0
				echo 'Cover resized successfully.'
			else
				echo 'This album doesn\'t have a cover.'
				exit 1
			end
		case '*'
			echo 'Invalid option, use -h for help.'
			exit 1
	end
else
	set path (mpc -f \%file\% | cut -d \n -f 1 | rev | cut -d / -f 2- | rev)

	pqiv $musicdir/$path/cover.*

	if test $status -ge 1
		echo 'This album doesn\'t have a cover.'
		exit 1
	end
end
