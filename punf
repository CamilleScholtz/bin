#!/bin/fish
#
# punf - Upload files/scrots/urls to sr.ht (rip pomf & uguu)
# onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Your sr.ht key
set key (cat $HOME/.srht)

# Log urls
set log false

# Set log location
set logfile $HOME/.punf

# Copy links to clipboard (requires xclip)
set clipboard true

# Send notification when upload is done (requires my notify script)
set notify true

# Open url in browser
set browser false


## FUNCTIONS

# This function sets $file to your clipboard contents
function clipboard
	set -g uploadme /tmp/clipboard.txt

	xclip -o > $uploadme

	set -g word clipboard
end

# This function sets $file to a selection scrot
function selection
	set -g uploadme /tmp/scrot.png

	maim --hidecursor -s -b 3 -c 250,250,250 $uploadme 2> /dev/null
	if test $status -ge 1
		echo "Selection cancelled."
		exit 1
	end

	set -g word selection
end

# This function sets $file to a url
function url
	set type (echo $argv | rev | cut -d . -f 1 | rev)
	set -g uploadme /tmp/url.$type

	wget --quiet $argv -O $uploadme

	set -g word url
end

# This function sets $file to a file
function file
	if test -f $argv
		set -g uploadme $argv
	else
		echo "File not found."
		exit 1
	end

	set -g word file
end

# This function sets $file to a full screen scrot
function desktop
	set -g uploadme /tmp/scrot.png

	maim --hidecursor $uploadme

	set -g word desktop
end

# This function uploads the $file
function upload
	set -g url (curl --silent -F key=$key -F file="@$uploadme" https://sr.ht/api/upload | grep -o -i "https://sr.ht/[a-z0-9.]*")
end

# This function logs the url,  copies the url to the clipboard, and/or opens the url in your browser
function feedback
	# Copy url to clipboard
	if begin test $clipboard = true; and test $quiet = false; end
		echo $url | xclip -selection primary
		echo $url | xclip -selection clipboard
	end

	# Log url
	if begin test $log = true; and test $quiet = false; end
		echo $url >> $logfile
	end

	# Open url in browser
	if begin test $browser = true; and test $quiet = false; end
		$BROWSER $url
	end

	# Send notification
	if begin test $notify = true; and test $quiet = false; end
		notify "Punfed $word: $url" &
	end
	
	# Send link
	if test $quiet = true
		echo $url
	else
		echo "Punfed $word: $url"
	end
end

## EXECUTE

set quiet false

if test (count $argv) -ge 1
	switch $argv[1]
		case -h --help
			echo -e "Usage: punf [options] [file/url]\n"
			echo "options:"
			echo "  -c,   --clipboard       upload your clipboard as text"
			echo "  -s,   --selection       upload selection scrot"
			echo "  -q,   --quiet           disable all feedback (for scripts using punf)"
			echo "  -v,   --version         print version and exit"
			echo "  -h,   --help            print help and exit"
			exit 0
		case -v --version
			echo "punf 0.9.2"
			exit 0
		case -c --clipboard
			clipboard
		case -s --selection
			selection
		case -q --quiet
			set quiet true
			file $argv[2]
		case "http*"
			url $argv[1]
		case "*"
			file $argv[1]
	end
else
	desktop
end

upload
feedback
