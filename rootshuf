#!/bin/fish
#
# rootshuf - Set random wallpapers.
# # onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set wallshuf location
set wallshufdir $HOME/.wallshuf

# Set start times, accepted wallpaper type (currently only one allowed), and background color
set morning 6 bright
set afternoon 13 bright
set night 20 dark

# Set highlight color
set highlight yellow


## FUNCTIONS

# This functions sets the wallpaper
function setrandomwall
	set dir (
		for directory in $argv
			echo $directory
		end | shuf -n 1
	)

	# Exit if the wallshuf dir does not exist
	if not test -d $wallshufdir/$dir
		echo "No wallpapers in "(set_color $highlight)"$wallshufdir/$dir"(set_color normal)"."
		exit 1
	end

	set wallpaper (ls $wallshufdir/$dir[1] | shuf -n 1)

	setroot -c $wallshufdir/$dir/$wallpaper
	echo "A $dir wallpaper has been set, "(set_color $highlight)"$wallpaper"(set_color normal)"."
end


## EXECUTE

if test (count $argv) -eq 1
	setrandomwall $argv
else
	# Get time
	set hour (date +%H)

	if test $hour -ge $morning[1] -a $hour -lt $afternoon[1]
		setrandomwall $morning[2..-1]
	else if test $hour -ge $afternoon[1] -a $hour -lt $night[1]
		setrandomwall $afternoon[2..-1]
	else
		setrandomwall $night[2..-1]
	end
end
