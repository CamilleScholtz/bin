#!/bin/fish
#
# rootshuf - Set random wallpapers.
# # onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set rootshuf location
set rootshufdir $HOME/.rootshuf

# Set start times and accepted root image types
set morning 6 bright
set afternoon 13 bright
set night 20 dark

# Set highlight color
set highlight yellow


## FUNCTIONS

# This functions sets the root image
function setrandomrootimage
	set type (
		for directory in $argv
			echo $directory
		end | shuf -n 1
	)

	# Exit if the rootshuf dir does not exist
	if not test -d $rootshufdir/$dir
		echo "No images in "(set_color $highlight)"$rootshufdir/$type"(set_color normal)"."
		exit 1
	end

	set image (ls $rootshufdir/$type | shuf -n 1)

	setroot -c $rootshufdir/$type/$image ^ /dev/null
	echo "A $type image has been set as the root image, "(set_color $highlight)"$image"(set_color normal)"."
end


## EXECUTE

if test (count $argv) -ge 1
	setrandomrootimage $argv
else
	# Get time
	set hour (date +%H)

	if test $hour -ge $morning[1] -a $hour -lt $afternoon[1]
		setrandomrootimage $morning[2..-1]
	else if test $hour -ge $afternoon[1] -a $hour -lt $night[1]
		setrandomrootimage $afternoon[2..-1]
	else
		setrandomrootimage $night[2..-1]
	end
end
